/*------------------------------------------------------------------------------
  한국 열차 세트(Korean Train Set)
  * Official download site : https://telk.kr/ottd/newgrf/ko_train_set
  * Github repository      : https://github.com/KoreanGRF/KoreanTrainSet
------------------------------------------------------------------------------*/
/**
 * K4x00
 */
// Defines
#define SPEED_K4x00           150
#define COST_K4x00            2
#define RUNNINGCOST_K4x00     10

switch(FEAT_TRAINS, SELF, var_speed_K4x00, 1)		{ return SPEED_K4x00 / 10 * coeff_speed_general; }
switch(FEAT_TRAINS, SELF, var_cost_K4x00, 1)		{ return COST_K4x00 / 10 * sw_cost_factor; }
switch(FEAT_TRAINS, SELF, var_runcost_K4x00, 1)		{ return RUNNINGCOST_K4x00 / 10 * sw_running_cost_factor; }

// 4x00용 템플릿
template tmpl_K4x00(x, y) {
	[x,      y,   8,  19,  -3,  -9]
	[x+15,   y,  18,  14, -11,  -7]
	[x+45,   y,  23,  12, -11,  -9]
	[x+95,   y,  18,  15,  -4,  -7]
	[x+125,  y,   8,  20,  -3, -10]
	[x+140,  y,  18,  15, -12,  -7]
	[x+175,  y,  23,  12, -12,  -9]
	[x+225,  y,  18,  14,  -5,  -7]
}
template tmpl_K4x00_purchase(x, y) {
	tmpl_purchase_detail(x, y, 50, 12, -25, -6)
}

// 4400호대
spriteset(set_K4x00_purchase,       "./src/locomotive/K4x00/K4x00.png") { tmpl_K4x00_purchase(0, 0) }
spriteset(set_K4x00A_loco,          "./src/locomotive/K4x00/K4x00.png") { tmpl_K4x00(60, 0) }   // Green & Yellow print
spriteset(set_K4x00B_loco,          "./src/locomotive/K4x00/K4x00.png") { tmpl_K4x00(60, 30) }   // Red & White & Blue print
spriteset(set_K4x00_VTRAIN_loco,    "./src/locomotive/K4x00/K4x00.png") { tmpl_K4x00(60, 60) }   // V-train

// Refit Paintings
switch(FEAT_TRAINS, SELF, sw_K4x00_loco, cargo_subtype) {
	0:	set_K4x00B_loco;
	1:	set_K4x00A_loco;
	10:	set_K4x00_VTRAIN_loco;
	set_K4x00B_loco;
}
switch(FEAT_TRAINS, SELF, sw_K4x00_cargo_subtype_text, cargo_subtype) {
	0:	return string(STR_REFIT_PAINTING_THESEDAYS);
	1:	return string(STR_REFIT_PAINTING_7x00_OLD_KORAIL);
	10:	return string(STR_REFIT_PAINTING_VTRAIN);
	return string(STR_REFIT_PAINTING_THESEDAYS);
}

// Item
item(FEAT_TRAINS, ko_train_K4x00, 55040) {
	property {
		// Common properties
		name:							string(STR_K4x00_NAME);
		climates_available:				ALL_CLIMATES;
		introduction_date:				date(1963, 1, 1);
		track_type:						RAIL;
		engine_class:					ENGINE_CLASS_DIESEL;
		sprite_id:						SPRITE_ID_NEW_TRAIN;
		length:							7;

		// Spec
		speed:							105 km/h;
		power:							2719 kW;
		weight:							88 ton;
		tractive_effort_coefficient:	0.25;
		air_drag_coefficient:			0.1;

		// Model
		model_life:						30;
		vehicle_life:					30;
		retire_early:					0;
		reliability_decay:				0;

		// Cargo
		cargo_capacity:					1;
		refittable_cargo_classes:		NO_CARGO_CLASS;
		non_refittable_cargo_classes:	NO_CARGO_CLASS;
		cargo_allow_refit:				[PASS];
		cargo_disallow_refit:			[];
		loading_speed:					0;

		// Costs
		cost_factor:					10;
		refit_cost:						0;
		running_cost_base:				RUNNING_COST_DIESEL;
		running_cost_factor:			150;

		// Flags
		ai_special_flag:				AI_FLAG_PASSENGER;
		misc_flags:						bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_FLIP);
		bitmask_vehicle_info:			0;
	}
	graphics {
		default:						sw_K4x00_loco;
		purchase:						set_K4x00_purchase;

		speed:							var_speed_K4x00;
		cost_factor:					var_cost_K4x00;
		running_cost_factor:			var_runcost_K4x00;
		cargo_capacity:					0;

		can_attach_wagon:				sw_GENE_attach;
		cargo_subtype_text:				sw_K4x00_cargo_subtype_text;
		additional_text:				string(STR_REFIT_ADDITIONAL_ABLE);
	}
}
// End of "K4x00"