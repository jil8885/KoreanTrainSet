/*------------------------------------------------------------------------------
  한국 열차 세트(Korean Train Set)
  * Official download site : https://telk.kr/ottd/newgrf/ko_train_set
  * Github repository      : https://github.com/KoreanGRF/KoreanTrainSet
------------------------------------------------------------------------------*/
/**
 * CDC
 */
GENERAL(CDC)
GENERAL(CDC_wagon)

// Graphics
spriteset(set_CDC_purchase, "./src/locomotive/CDC/CDC.png") {
    tmpl_purchase_for_dualhead(0, 0)
}

// Garden (꽃동네)
spriteset(set_CDC_GARDEN_engineH, "./src/locomotive/CDC/CDC.png") {
    tmpl_CDC(60, 0)
}
spriteset(set_CDC_GARDEN_engineT, "./src/locomotive/CDC/CDC.png") {
    tmpl_CDC(60, 0)
}
spriteset(set_CDC_GARDEN_wagon, "./src/locomotive/CDC/CDC.png") {
    tmpl_wagon_by_coor(340, 0)
}

// Dolphin (돌고래)
spriteset(set_CDC_DOLPHIN_engineH, "./src/locomotive/CDC/CDC.png") {
    tmpl_CDC(60, 30)
}        
spriteset(set_CDC_DOLPHIN_engineT, "./src/locomotive/CDC/CDC.png") {
    tmpl_CDC(60, 30)
}
spriteset(set_CDC_DOLPHIN_wagon, "./src/locomotive/CDC/CDC.png") {
    tmpl_wagon_by_coor(340, 30)
}

// Seaside (바다열차 청색)
spriteset(set_CDC_SEASIDE_BLUE_engineH, "./src/locomotive/CDC/CDC.png") {
    tmpl_CDC(60, 60)
}
spriteset(set_CDC_SEASIDE_BLUE_engineT, "./src/locomotive/CDC/CDC.png") {
    tmpl_CDC(60, 60)
}
spriteset(set_CDC_SEASIDE_BLUE_wagon, "./src/locomotive/CDC/CDC.png") {
    tmpl_wagon_by_coor(340, 60)
}

// Seaside (바다열차: 적색 & 흰색)
spriteset(set_CDC_SEASIDE_RED_WHITE_engineH, "./src/locomotive/CDC/CDC.png") {
    tmpl_CDC(60, 90)
}
spriteset(set_CDC_SEASIDE_RED_WHITE_engineT, "./src/locomotive/CDC/CDC.png") {
    tmpl_CDC(60, 90)
}
spriteset(set_CDC_SEASIDE_RED_WHITE_wagon, "./src/locomotive/CDC/CDC.png") {
    tmpl_wagon_by_coor(340, 90)
}

// Baekje (백제왕관)
spriteset(set_CDC_BAEKJE_engineH, "./src/locomotive/CDC/CDC.png") {
    tmpl_CDC(60, 120)
}
spriteset(set_CDC_BAEKJE_engineT, "./src/locomotive/CDC/CDC.png") {
    tmpl_CDC(60, 120)
}
spriteset(set_CDC_BAEKJE_wagon, "./src/locomotive/CDC/CDC.png") {
    tmpl_wagon_by_coor(340, 120)
}

// KNR: Green & Yellow (구 철도청: 녹색 & 황색)
spriteset(set_CDC_GREEN_YELLOW_engineH, "./src/locomotive/CDC/CDC.png") {
    tmpl_CDC(60, 150)
}
spriteset(set_CDC_GREEN_YELLOW_engineT, "./src/locomotive/CDC/CDC.png") {
    tmpl_CDC(60, 150)
}
spriteset(set_CDC_GREEN_YELLOW_wagon, "./src/locomotive/CDC/CDC.png") {
    tmpl_wagon_by_coor(340, 150)
}

// RDC Mugunghwa (RDC 무궁화)
spriteset(set_CDC_MUGUNGHWA_engineH, "./src/locomotive/CDC/CDC.png") {
    tmpl_CDC(60, 180)
}
spriteset(set_CDC_MUGUNGHWA_engineT, "./src/locomotive/CDC/CDC.png") {
    tmpl_CDC(60, 180)
}
spriteset(set_CDC_MUGUNGHWA_wagon, "./src/locomotive/CDC/CDC.png") {
    tmpl_wagon_by_coor(340, 180)
}

// Current (현재 도색)
spriteset(set_CDC_engineH, "./src/locomotive/CDC/CDC.png") {
    tmpl_CDC(60, 210)
}
spriteset(set_CDC_engineT, "./src/locomotive/CDC/CDC.png") {
    tmpl_CDC(60, 210)
}
spriteset(set_CDC_wagon, "./src/locomotive/CDC/CDC.png") {
    tmpl_wagon_by_coor(340, 210)
}

// DMZ-train
spriteset(set_CDC_DMZTRAIN_engineH, "./src/locomotive/CDC/CDC.png") {
    tmpl_CDC(60, 240)
}
spriteset(set_CDC_DMZTRAIN_engineT, "./src/locomotive/CDC/CDC.png") {
    tmpl_CDC(60, 270)
}
spriteset(set_CDC_DMZTRAIN_wagon, "./src/locomotive/CDC/CDC.png") {
    tmpl_wagon_by_coor(340, 240)
}

// Gyeongbuk theme (경북순환테마열차(구))
spriteset(set_CDC_GYEONGBUK1_engineH, "./src/locomotive/CDC/CDC.png") {
    tmpl_CDC(60, 300)
}
spriteset(set_CDC_GYEONGBUK1_engineT, "./src/locomotive/CDC/CDC.png") {
    tmpl_CDC(60, 330)
}
spriteset(set_CDC_GYEONGBUK1_wagon1, "./src/locomotive/CDC/CDC.png") {
    tmpl_wagon_by_coor(340, 300)
}
spriteset(set_CDC_GYEONGBUK1_wagon2, "./src/locomotive/CDC/CDC.png") {
    tmpl_wagon_by_coor(340, 330)
}

// Gyeongbuk theme (경북순환테마열차(신))
spriteset(set_CDC_GYEONGBUK2_engineH, "./src/locomotive/CDC/CDC.png") {
    tmpl_CDC(60, 360)
}
spriteset(set_CDC_GYEONGBUK2_engineT, "./src/locomotive/CDC/CDC.png") {
    tmpl_CDC(60, 390)
}
spriteset(set_CDC_GYEONGBUK2_wagon1, "./src/locomotive/CDC/CDC.png") {
    tmpl_wagon_by_coor(340, 360)
}
spriteset(set_CDC_GYEONGBUK2_wagon2, "./src/locomotive/CDC/CDC.png") {
    tmpl_wagon_by_coor(340, 390)
}

// RDC 카페 객차
// spriteset(set_CDC_MUGUNGHWA_CAFE_wagon, "./src/locomotive/CDC/CDC.png") {
//     [560,   260,   8,    22,   -3,   -11]
//     [575,   260,   21,   16,   -14,   -8]
//     [605,   260,   31,   12,   -17,   -8]
//     [655,   260,   21,   16,   -6,    -8]
//     [685,   260,   8,    22,   -3,   -11]
//     [700,   260,   21,   16,   -14,   -8]
//     [735,   260,   31,   12,   -17,   -8]
//     [785,   260,   21,   16,   -6,    -8]
// }

// for Gyeongbuk theme (경북순환테마열차용)
switch(FEAT_TRAINS, SELF, sw_CDC_GYEONGBUK1_wagon, position_in_consist % 2) {
	1: set_CDC_GYEONGBUK1_wagon2;
	set_CDC_GYEONGBUK1_wagon1;
}
switch(FEAT_TRAINS, SELF, sw_CDC_GYEONGBUK2_wagon, position_in_consist % 2) {
	1: set_CDC_GYEONGBUK2_wagon2;
	set_CDC_GYEONGBUK2_wagon1;
}


// Refit Paintings
switch(FEAT_TRAINS, SELF, sw_CDC_engineH, cargo_subtype) {
	1:  set_CDC_GARDEN_engineH;
	2:  set_CDC_DOLPHIN_engineH;
	3:  set_CDC_SEASIDE_BLUE_engineH;
	4:  set_CDC_SEASIDE_RED_WHITE_engineH;
	5:  set_CDC_BAEKJE_engineH;
	6:  set_CDC_GREEN_YELLOW_engineH;
	7:  set_CDC_DMZTRAIN_engineH;
	8:  set_CDC_MUGUNGHWA_engineH;
	9:  set_CDC_GYEONGBUK1_engineH;
	10: set_CDC_GYEONGBUK2_engineH;
	set_CDC_engineH;
}
switch(FEAT_TRAINS, SELF, sw_CDC_engineT, cargo_subtype) {
	1:  set_CDC_GARDEN_engineT;
	2:  set_CDC_DOLPHIN_engineT;
	3:  set_CDC_SEASIDE_BLUE_engineT;
	4:  set_CDC_SEASIDE_RED_WHITE_engineT;
	5:  set_CDC_BAEKJE_engineT;
	6:  set_CDC_GREEN_YELLOW_engineT;
	7:  set_CDC_DMZTRAIN_engineT;
	8:  set_CDC_MUGUNGHWA_engineT;
	9:  set_CDC_GYEONGBUK1_engineT;
	10: set_CDC_GYEONGBUK2_engineT;
	set_CDC_engineT;
}
switch(FEAT_TRAINS, SELF, sw_CDC_engine, position_in_consist_from_end == 0 && num_vehs_in_consist > 1) {
	1: sw_CDC_engineT;
	sw_CDC_engineH;
}

switch(FEAT_TRAINS, SELF, sw_CDC_cargo_subtype_text, cargo_subtype) {
    1:    return string(STR_REFIT_PAINTING_CDC_GARDEN);            // Garden (꽃동네)
    2:    return string(STR_REFIT_PAINTING_CDC_DOLPHIN);           // Dolphin (돌고래)
    3:    return string(STR_REFIT_PAINTING_CDC_SEASIDE_BLUE);      // Seaside (바다열차(청))
    4:    return string(STR_REFIT_PAINTING_CDC_SEASIDE_RED_WHITE); // Seaside (바다열차(적+백))
    5:    return string(STR_REFIT_PAINTING_CDC_BAEKJE);            // Baekje (백제관광)
    6:    return string(STR_REFIT_PAINTING_GREEN_YELLOW);          // 구 철도청(녹+황)
    7:    return string(STR_REFIT_PAINTING_CDC_DMZTRAIN);          // DMZ-train
    8:    return string(STR_REFIT_PAINTING_CDC_MUGUNGHWA);         // RDC
    9:    return string(STR_REFIT_PAINTING_CDC_GYEONGBUK1);        // 경북관광열차#1
    10:    return string(STR_REFIT_PAINTING_CDC_GYEONGBUK2);       // 경북관광열차#2
    return string(STR_REFIT_PAINTING_THESEDAYS);
}

// Wagon
switch(FEAT_TRAINS, PARENT, sw_CDC_wagon_by_parent, cargo_subtype) {
	1:  set_CDC_GARDEN_wagon;
	2:  set_CDC_DOLPHIN_wagon;
	3:  set_CDC_SEASIDE_BLUE_wagon;
	4:  set_CDC_SEASIDE_RED_WHITE_wagon;
	5:  set_CDC_BAEKJE_wagon;
	6:  set_CDC_GREEN_YELLOW_wagon;
	7:  set_CDC_DMZTRAIN_wagon;
	8:  set_CDC_MUGUNGHWA_wagon;
	9:  sw_CDC_GYEONGBUK1_wagon;
	10: sw_CDC_GYEONGBUK2_wagon;
	set_CDC_wagon;
}
switch(FEAT_TRAINS, SELF, sw_CDC_wagon, cargo_subtype) {
	1:  set_CDC_GARDEN_wagon;
	2:  set_CDC_DOLPHIN_wagon;
	3:  set_CDC_SEASIDE_BLUE_wagon;
	4:  set_CDC_SEASIDE_RED_WHITE_wagon;
	5:  set_CDC_BAEKJE_wagon;
	6:  set_CDC_GREEN_YELLOW_wagon;
	7:  set_CDC_DMZTRAIN_wagon;
	8:  set_CDC_MUGUNGHWA_wagon;
	9:  sw_CDC_GYEONGBUK1_wagon;
	10: sw_CDC_GYEONGBUK2_wagon;
	sw_CDC_wagon_by_parent;
}

// Item
item(FEAT_TRAINS, ko_train_CDC, 55160) {
    property {
        // Common properties
        name:                           string(STR_CDC_NAME);
        climates_available:             ALL_CLIMATES;
        introduction_date:              var_CDC_INTRODUCTION;
        track_type:                     RAIL;
        engine_class:                   ENGINE_CLASS_DIESEL;
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        dual_headed:                    1;

        // Spec
        speed:                          120 km/h;
        power:                          800 kW;
        weight:                         17 ton;
        tractive_effort_coefficient:    0.25;
        air_drag_coefficient:           0.1;

        // Model
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        retire_early:                   0;
        reliability_decay:              0;

        // Cargo
        cargo_capacity:                 50;
        refittable_cargo_classes:       NO_CARGO_CLASS;
        non_refittable_cargo_classes:   NO_CARGO_CLASS;
        cargo_allow_refit:              [PASS];
        cargo_disallow_refit:           [];
        loading_speed:                  20;

        // Costs
        cost_factor:                    10;
        refit_cost:                     0;
        running_cost_base:              RUNNING_COST_DIESEL;
        running_cost_factor:            150;

        // Flags
        ai_special_flag:                AI_FLAG_PASSENGER;
        misc_flags:                     bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_FLIP);
        bitmask_vehicle_info:           0;
        visual_effect_and_powered:      VISUAL_EFFECT_DIESEL;
    }
    graphics {
        default:                        sw_CDC_engine;
        purchase:                       set_CDC_purchase;

        speed:                          sw_CDC_speed;
        cost_factor:                    sw_CDC_cost;
        running_cost_factor:            sw_CDC_runningcost;
        cargo_capacity:                 sw_CDC_capacity;
        loading_speed:                  sw_CDC_loadingspeed;

        can_attach_wagon:               sw_GENE_attach;
        start_stop:                     sw_ko_train_start_stop;
        cargo_subtype_text:             sw_CDC_cargo_subtype_text;
        additional_text:                string(STR_REFIT_ADDITIONAL_ABLE);
    }
    livery_override(ko_train_PASSWG) {
        default:                        sw_CDC_wagon;
        cargo_capacity:                 sw_CDC_wagon_capacity;
    }
    livery_override(ko_train_PASSWGP) {
        default:                        sw_CDC_wagon;
        cargo_capacity:                 sw_CDC_wagon_capacity;
    }
}
// End of "CDC"