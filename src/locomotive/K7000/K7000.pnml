/*------------------------------------------------------------------------------
  한국 열차 세트(Korean Train Set)
  * Official download site : https://telk.kr/ottd/newgrf/ko_train_set
  * Github repository      : https://github.com/KoreanGRF/KoreanTrainSet
------------------------------------------------------------------------------*/
/**
 * K7000
 */
// Defines
#define SPEED_K7000           150
#define COST_K7000            2
#define RUNNINGCOST_K7000     10

switch(FEAT_TRAINS, SELF, var_speed_K7000, 1)   { return SPEED_K7000 / 10 * coeff_speed_general; }
switch(FEAT_TRAINS, SELF, var_cost_K7000, 1)    { return COST_K7000 / 10 * sw_cost_factor; }
switch(FEAT_TRAINS, SELF, var_runcost_K7000, 1) { return RUNNINGCOST_K7000 / 10 * sw_running_cost_factor; }


// 7000 (봉고)
spriteset(set_K7000_purchase, "./src/locomotive/K7000/K7000.png") {
	tmpl_purchase_detail(0, 0, 50, 12, -25, -6)
}
spriteset(set_K7000A_loco, "./src/locomotive/K7000/K7000.png") {   // Red & White & Blue
	[60,   0,  8,   22,   -3,   -9]
	[75,   0,  21,  16,  -14,   -7]
	[105,  0,  30,  12,  -17,   -8]
	[155,  0,  21,  16,   -5,   -8]
	[185,  0,  8,   22,   -3,  -11]
	[200,  0,  21,  16,  -14,   -8]
	[235,  0,  30,  12,  -16,   -8]
	[285,  0,  21,  16,   -6,   -7]
}

/*------------------------------------------------------------------------------
K7000
------------------------------------------------------------------------------*/
/*
//TODO// Refit Paintings
*/

item(FEAT_TRAINS, ko_train_K7000, 55080) {
    property {
        // Common properties
        name:                           string(STR_K7000_NAME);
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1986, 1, 1);
        track_type:                     RAIL;
        engine_class:                   ENGINE_CLASS_DIESEL;
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        length:                         7;

        // Spec
        speed:                          150 km/h;
        power:                          2719 kW;
        weight:                         119 ton;
        tractive_effort_coefficient:    0.25;
        air_drag_coefficient:           0.1;

        // Model
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        retire_early:                   0;
        reliability_decay:              0;

        // Cargo
        cargo_capacity:                 1;
        refittable_cargo_classes:       NO_CARGO_CLASS;
        non_refittable_cargo_classes:   NO_CARGO_CLASS;
        cargo_allow_refit:              [PASS];
        cargo_disallow_refit:           [];
        loading_speed:                  0;

        // Costs
        cost_factor:                    10;
        refit_cost:                     0;
        running_cost_base:              RUNNING_COST_DIESEL;
        running_cost_factor:            150;

        // Flags
        ai_special_flag:                AI_FLAG_PASSENGER;
        misc_flags:                     bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_FLIP);
        bitmask_vehicle_info:           0;
    }
    graphics {
        default:                        set_K7000A_loco;
        purchase:                       set_K7000_purchase;

        speed:                          var_speed_K7000;
        cost_factor:                    var_cost_K7000;
        running_cost_factor:            var_runcost_K7000;
        cargo_capacity:                 0;

        can_attach_wagon:               sw_GENE_attach;
        // cargo_subtype_text:          sw_K7000_cargo_subtype_text;
        additional_text:                string(STR_REFIT_ADDITIONAL_UNABLE);
    }
}
// End of "K7000"